apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'
    defaultConfig {
        applicationId "com.modular"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-annotations:24.2.1"
    compile "com.android.support:recyclerview-v7:24.2.1"
    compile "com.android.support:cardview-v7:24.2.1"
    compile "com.android.support:appcompat-v7:24.2.1"
    compile "com.android.support:design:24.2.1"
    compile "com.google.guava:guava:18.0"
    compile "com.android.support.test.espresso:espresso-idling-resource:2.2.2"

    compile "com.google.dagger:dagger:2.2"
    annotationProcessor "com.google.dagger:dagger-compiler:2.2"
    provided "org.glassfish:javax.annotation:10.0-b28"
    compile "com.jakewharton:butterknife:8.4.0"
    annotationProcessor "com.jakewharton:butterknife-compiler:8.4.0"

    compile "com.squareup.okhttp3:okhttp:3.4.1"
    compile "com.squareup.okhttp3:logging-interceptor:3.4.1"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.1"

    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "com.squareup.retrofit2:converter-moshi:2.1.0"
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"

    compile "io.reactivex:rxjava:1.1.6"
    compile "io.reactivex:rxandroid:1.2.1"

    compile "com.jakewharton.timber:timber:4.3.1"
    compile "com.jakewharton.threetenabp:threetenabp:1.0.4"

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.hamcrest:hamcrest-all:1.3"

    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"

    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:2.2.2"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"
    androidTestCompile "com.android.support:appcompat-v7:24.2.1"
    androidTestCompile "com.android.support:recyclerview-v7:24.2.1"
    androidTestCompile "com.android.support:design:24.2.1"
}
